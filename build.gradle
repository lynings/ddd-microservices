plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '2.1.6.RELEASE'
}

repositories {
    mavenLocal()
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter' }
    mavenCentral()
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    group 'pers.lyning.medical'
    version '1.0'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

subprojects {
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR2'
        }
    }

    repositories {
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter' }
        mavenCentral()
    }

    dependencies {
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
        annotationProcessor 'org.projectlombok:lombok:1.18.8'
    }


    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }
}

["order-context", "patient-context", "nursing-context", "nurse-context"].each { service ->
    project(":$service") {
        apply plugin: 'application'

        dependencies {
            runtime 'mysql:mysql-connector-java:5.1.47'

            compile 'org.flywaydb:flyway-core:5.2.4',
                    // 依赖 ddd 规范
                    project(":core-standard")
            
            
            testCompile ('org.springframework.boot:spring-boot-starter-test') {
                exclude group: 'junit', module: 'junit;'
            }
            
            testCompile 'com.h2database:h2:1.4.199',
                    
                    'org.junit.jupiter:junit-jupiter-api',
                    'org.junit.jupiter:junit-jupiter-params',
                    'org.junit.jupiter:junit-jupiter-engine',
                    
                    'org.springframework.restdocs:spring-restdocs-mockmvc',
                    'org.springframework.restdocs:spring-restdocs-webtestclient'
        }
    }
}