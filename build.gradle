plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '2.1.6.RELEASE'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenLocal()
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter' }
    mavenCentral()
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    group 'pers.lyning.springcloud'
    version '1.0'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

subprojects {
    apply plugin: 'application'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("$buildDir/classes/java/main/")
            testOutputDir = file("$buildDir/classes/java/test/")
        }
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR2'
        }
    }

    repositories {
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter' }
        mavenCentral()
    }

    dependencies {
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
        annotationProcessor 'org.projectlombok:lombok:1.18.8'
    }


    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }
}

["ordercontext", "usercontext"].each { service ->
    project(":$service") {
        dependencies {
            runtime 'mysql:mysql-connector-java:5.1.47'

            compile 'org.flywaydb:flyway-core:5.2.4',
                    // 依赖 ddd 规范
                    project(":core-standard")

            testCompile 'com.h2database:h2:1.4.199',
                    'org.junit.jupiter:junit-jupiter-api',
                    'org.junit.jupiter:junit-jupiter-params',
                    'org.junit.jupiter:junit-jupiter-engine',
                    'org.springframework.boot:spring-boot-starter-test'
        }

        // 集成测试配置
        apply from: rootProject.file('integration-test.gradle')
    }
}
